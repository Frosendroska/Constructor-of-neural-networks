plugins {
  id 'org.jetbrains.kotlin.jvm'
  id 'org.jetbrains.kotlin.plugin.spring'
  id 'org.springframework.boot'
  id 'io.spring.dependency-management'
}

dependencies {
  implementation project(':nnbuilder-api')

  implementation 'org.springframework.boot:spring-boot-starter-webflux'
  implementation 'org.jetbrains.kotlin:kotlin-reflect'
  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

  implementation 'io.grpc:grpc-kotlin-stub:1.1.0'
  implementation 'net.devh:grpc-server-spring-boot-starter:2.12.0.RELEASE'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

task bootRunDev(type: org.springframework.boot.gradle.tasks.run.BootRun) {
  group = 'Application'

  doFirst() {
    main = bootJar.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    systemProperty 'spring.profiles.active', 'dev'
  }
}

tasks.named('test') {
  useJUnitPlatform()
}

spotless {
  java {
    palantirJavaFormat()
  }
  kotlin {
    ktlint()
  }
}

bootJar {
  dependsOn ':frontend:assemble'
  from('../frontend/build/dist') {
    into 'static'
  }
}
